buildscript {
    ext.kotlinVersion = '1.3.72'
    repositories {
        google()
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:4.1.0-beta02'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"
        classpath "org.jetbrains.kotlin:kotlin-serialization:$kotlinVersion"
    }
}

allprojects {
    repositories {
        google()
        jcenter()
        flatDir { dirs 'libs' }
        maven { url 'https://dl.bintray.com/ironsource-mobile/android-adapters' }
        maven { url 'https://dl.bintray.com/ironsource-mobile/android-sdk' }
    }
}

ext {
    compileSdkVersion = 30
    buildToolsVersion = '30.0.1'
    targetSdkVersion = 30
    buildPrefab = true
}

def applyPrefabSettings(project, String libraryName, headerPaths) {
    def projectDir = project.projectDir
    project.android {
        ndkVersion '21.3.6528147'

        buildFeatures {
            prefabPublishing true
        }

        externalNativeBuild {
            cmake {
                path "$projectDir/CMakeLists.txt"
            }
        }

        prefab {
            "$libraryName" {
                headers "$projectDir/.generated_headers"
            }
        }
    }

    project.preBuild.dependsOn(tasks.create(name: "${libraryName}CopyHeaders") {
        doFirst {
            delete {
                delete "$projectDir/.generated_headers"
            }
            copy {
                from '../src'
                into "$projectDir/.generated_headers"
                headerPaths.each { item ->
                    include item
                }
            }
        }
    })
}