buildscript {
    ext.kotlinVersion = '1.4.10'
    repositories {
        google()
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:4.0.1'
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.8.5'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"
        classpath "org.jetbrains.kotlin:kotlin-serialization:$kotlinVersion"
    }
}

allprojects {
    repositories {
        google()
        jcenter()
        flatDir { dirs 'libs' }
        maven { url 'https://dl.bintray.com/enrevol/ee-x' }
        maven { url 'https://dl.bintray.com/ironsource-mobile/android-adapters' }
        maven { url 'https://dl.bintray.com/ironsource-mobile/android-sdk' }
    }
}

ext {
    compileSdkVersion = 30
    buildToolsVersion = '30.0.2'
    targetSdkVersion = 30
    buildPrefab = true
    buildMaven = true
    plugins = [
        'core'                  : ['1.1.1'],
        'ads'                   : ['1.1.1', 'core'],
        'admob'                 : ['1.1.2', 'ads'],
        'admob-mediation'       : ['1.1.2', 'admob'],
        'app-lovin'             : ['1.1.2', 'ads'],
        'facebook-ads'          : ['1.1.3', 'ads'],
        'iron-source'           : ['1.1.2', 'ads'],
        'iron-source-mediation' : ['1.1.2', 'iron-source'],
        'unity-ads'             : ['1.1.2', 'ads'],
        'vungle'                : ['1.1.1', 'ads'],
        'adjust'                : ['1.1.1', 'core'],
        'apps-flyer'            : ['1.1.1', 'core'],
        'campaign-receiver'     : ['1.1.1', 'core'],
        'facebook'              : ['1.1.1', 'core'],
        'firebase-core'         : ['1.1.1', 'core'],
        'firebase-analytics'    : ['1.1.1', 'firebase-core'],
        'firebase-crashlytics'  : ['1.1.1', 'firebase-core'],
        'firebase-dynamic-link' : ['1.1.1', 'firebase-core'],
        'firebase-messaging'    : ['1.1.1', 'firebase-core'],
        'firebase-remote-config': ['1.1.1', 'firebase-core'],
        'firebase-storage'      : ['1.1.1', 'firebase-core'],
        'firebase-performance'  : ['1.1.1', 'firebase-core'],
        'google-analytics'      : ['1.1.1', 'core'],
        'notification'          : ['1.1.1', 'core'],
        'play'                  : ['1.1.1', 'core'],
        'recorder'              : ['1.1.1', 'core'],
        'soomla-store'          : ['1.1.1', 'store'],
        'store'                 : ['1.1.1', 'core'],
    ]
}

def applyPrefabSettings(project, String libraryName, headerPaths) {
    /*
    Wait for AndroidStudio4.1
    
    def projectDir = project.projectDir
    project.android {
        ndkVersion '21.3.6528147'

        buildFeatures {
            prefabPublishing true
        }

        externalNativeBuild {
            cmake {
                path "$projectDir/CMakeLists.txt"
            }
        }

        prefab {
            "$libraryName" {
                headers "$projectDir/.generated_headers"
            }
        }
    }

    project.preBuild.dependsOn(tasks.create(name: "${libraryName}CopyHeaders") {
        doFirst {
            delete {
                delete "$projectDir/.generated_headers"
            }
            copy {
                from '../src'
                into "$projectDir/.generated_headers"
                headerPaths.each { item ->
                    include item
                }
            }
        }
    })
     */
}

def configureAndPublish(String libraryName) {
    project("ee-x-$libraryName") {
        def plugins = rootProject.ext.plugins[libraryName]
        for (int i = 1; i < plugins.size(); ++i) {
            def plugin = plugins[i]
            dependencies {
                api "com.senspark.ee:$plugin:${rootProject.ext.plugins[plugin][0]}"
            }
        }
        publishToMaven(libraryName, plugins[0])
    }
}

def publishToMaven(String libraryName, String libraryVersion) {
    project("ee-x-$libraryName") {
        apply plugin: 'com.jfrog.bintray'
        apply plugin: 'maven-publish'
        task androidSourcesJar(type: Jar) {
            from android.sourceSets.main.java.srcDirs
            archiveClassifier.set('sources')
        }
        afterEvaluate {
            publishing {
                publications {
                    create(libraryName, MavenPublication) {
                        from project.components.release
                        artifact androidSourcesJar
                        groupId = 'com.senspark.ee'
                        artifactId = libraryName
                        version = libraryVersion
                    }
                }
                repositories {
                    maven {
                        url "$buildDir/repo"
                    }
                }
            }
            bintray {
                user = "${BINTRAY_USER}"
                key = "${BINTRAY_API_KEY}"
                override = true
                publications = [libraryName]
                pkg {
                    repo = 'ee-x'
                    name = libraryName
                    licenses = ['MIT']
                    publish = true
                    vcsUrl = 'https://github.com/Senspark/ee-x.git'
                    version {
                        name = libraryVersion
                    }
                }
            }
        }
    }
}