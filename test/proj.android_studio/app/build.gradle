apply plugin: 'com.android.application'
apply plugin: 'io.fabric'

android {
    compileSdkVersion 25
    buildToolsVersion '25.0.3'
    flavorDimensions 'screenSize'

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    defaultConfig {
        minSdkVersion 14
        targetSdkVersion 25

        applicationId 'com.ee.test.android'
        versionCode 1
        versionName '1.0.0'

        // API key used for testing.
        manifestPlaceholders = [fabric_api_key: 'e5208870d63af3a8614d3a13ca3d7f76a2104a27']

        externalNativeBuild {
            ndkBuild {
                targets 'ee_x_test_android_shared'

                arguments 'NDK_TOOLCHAIN_VERSION=clang'
                arguments 'APP_STL=c++_static'
                arguments 'APP_PLATFORM=9'
                arguments 'APP_LDFLAGS:=-latomic'
                arguments 'APP_LDFLAGS+=-llog'
                arguments '-j' + Runtime.runtime.availableProcessors()

                cppFlags += '-std=c++1z'
                cppFlags += '-fexceptions'
                cppFlags += '-fsigned-char'

                abiFilters 'armeabi', 'armeabi-v7a', 'x86'
            }
        }
    }

    externalNativeBuild {
        ndkBuild {
            path 'src/main/jni/Android.mk'
        }
    }

    buildTypes {
        debug {
            debuggable true
            jniDebuggable true
            minifyEnabled false

            externalNativeBuild {
                ndkBuild {
                    arguments 'NDK_DEBUG=0'
                }
            }
        }
        release {
            debuggable false
            jniDebuggable false
            minifyEnabled false

            externalNativeBuild {
                ndkBuild {
                    arguments 'NDK_DEBUG=1'
                }
            }
        }
    }

    sourceSets.main {
        jni.srcDirs = []
        jniLibs.srcDirs 'src/main/libs', 'src/main/jniLibs'
    }

    splits {
        abi {
            enable true
            reset()
            include 'armeabi', 'armeabi-v7a', 'x86'
        }
    }

    packagingOptions {
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
    }
}

crashlytics {
    enableNdk true
    androidNdkOut 'src/main/obj'
    androidNdkLibsOut 'src/main/libs'
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])

    compile project(':ee-x-notification')
    compile project(':ee-x-crashlytics')
    compile project(':ee-x-firebase')

    compile 'com.android.support:appcompat-v7:25.3.1'
    compile 'com.android.support:multidex:1.0.1'
}

apply plugin: 'com.google.gms.google-services'
