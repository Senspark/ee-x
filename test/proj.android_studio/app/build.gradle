apply plugin: 'com.android.application'
apply plugin: 'io.fabric'

android {
    compileSdkVersion 26
    buildToolsVersion '26.0.0'
    flavorDimensions 'screenSize'

    defaultConfig {
        minSdkVersion 14
        targetSdkVersion 26

        applicationId 'com.ee.test.android'
        versionCode 1
        versionName '1.0.0'

        // API key used for testing.
        manifestPlaceholders = [fabric_api_key: '45063244ecb24aee8a3b252cb5c6a2c549cf91f9']

        externalNativeBuild {
            ndkBuild {
                targets 'ee_x_test_android_shared'

                arguments 'NDK_TOOLCHAIN_VERSION=clang'
                arguments 'APP_STL=c++_static'
                arguments 'APP_PLATFORM=9'
                arguments 'APP_LDFLAGS:=-latomic'
                arguments 'APP_LDFLAGS+=-llog'

                // http://stackoverflow.com/questions/42579759/how-to-change-android-gradle-ndk-j-jobs-flag-value-for-clean-task
                // arguments '-j' + Runtime.runtime.availableProcessors()
                arguments '-j1'

                cppFlags += '-std=c++1z'
                cppFlags += '-fexceptions'
                cppFlags += '-fsigned-char'

                abiFilters 'armeabi', 'armeabi-v7a', 'x86'
            }
        }
    }

    externalNativeBuild {
        ndkBuild {
            path 'src/main/jni/Android.mk'
        }
    }

    buildTypes {
        debug {
            debuggable true
            jniDebuggable true
            minifyEnabled false

            externalNativeBuild {
                ndkBuild {
                    arguments 'NDK_DEBUG=0'
                }
            }
        }
        release {
            debuggable false
            jniDebuggable false
            minifyEnabled false

            externalNativeBuild {
                ndkBuild {
                    arguments 'NDK_DEBUG=1'
                }
            }
        }
    }

    splits {
        abi {
            enable true
            reset()
            include 'armeabi', 'armeabi-v7a', 'x86'
        }
    }

    packagingOptions {
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
    }
}

crashlytics {
    enableNdk true
    androidNdkOut 'src/main/obj'
    androidNdkLibsOut 'src/main/libs'
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])

    compile project(':ee-x-notification')
    compile project(':ee-x-crashlytics')
    compile project(':ee-x-firebase')
    compile project(':ee-x-facebook-ads')
    compile project(':ee-x-unity-ads')
    compile project(':ee-x-vungle')
    compile project(':ee-x-ironsrc')

    compile 'com.android.support:appcompat-v7:26.0.0'
    compile 'com.android.support:multidex:1.0.1'

    compile 'com.google.android.gms:play-services-base:11.0.4'
}

apply plugin: 'com.google.gms.google-services'
